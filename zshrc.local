# Corrects issue with Cygwin failing to install Vim plugins through rcup
[[ -d "/cygdrive/c/cygwin/bin" ]] && PATH="/cygdrive/c/cygwin/bin":${PATH}

# User-Specific Binaries
[[ -d "${HOME}/bin" ]] && PATH="${HOME}/bin":${PATH}

# Make typed commands cyan for easy scanning and readability
zle_highlight=(default:fg=cyan)

# Use my customized fancy zsh-git-prompt
GIT_PROMPT_EXECUTABLE="haskell"
[[ -d "${HOME}/tools/zsh-git-prompt" ]] && \
  source "${HOME}/tools/zsh-git-prompt/zsh-git-prompt.sh"
PROMPT='%F{green}%m%f %B%F{yellow}%~%f%b %# '

# Make right prompt asynchronous, so that the shell remains
# responsive even in large and slow Git working copies.
# http://www.anishathalye.com/2015/02/07/an-asynchronous-shell-prompt/

# enable command substition in prompt
setopt prompt_subst

RPROMPT='' # no initial prompt, set dynamically

ASYNC_PROC=0
function precmd() {
    function async() {
        # save to temp file
        printf "%s" "$(git_super_status)" > "/tmp/zsh_prompt_$$"

        # signal parent
        kill -s USR1 $$
    }

    # do not clear RPROMPT, let it persist

    # kill child if necessary
    if [[ "${ASYNC_PROC}" != 0 ]]; then
        kill -s HUP $ASYNC_PROC >/dev/null 2>&1 || :
    fi

    # start background computation
    async &!
    ASYNC_PROC=$!
}

function TRAPUSR1() {
    # read from temp file
    RPROMPT="$(cat /tmp/zsh_prompt_$$)"

    # reset proc number
    ASYNC_PROC=0

    # redisplay
    zle && zle reset-prompt
}
